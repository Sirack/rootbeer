apply plugin: 'com.android.application'
apply plugin: 'maven'

android {
    compileSdkVersion compileSdkVer
//    buildToolsVersion buildToolsVer

    defaultConfig {
        applicationId "com.scottyab.rootbeer.sample"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 4
        versionName "0.4"
        minSdkVersion 14
        targetSdkVersion 27
    }

    //check if the keystore details are defined in gradle.properties (this is so the key is not in github)
    if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
        signingConfigs {
            //from ~/user/.gradle/gradle.properties
            release {
                storeFile file(ROOTBEER_SAMPLE_STORE)
                keyAlias ROOTBEER_SAMPLE_KEY
                storePassword ROOTBEER_SAMPLE_STORE_PASS
                keyPassword ROOTBEER_SAMPLE_KEY_PASS
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        release {
            if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions{
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':rootbeerlib')
    compile "com.android.support:appcompat-v7:$supportLibVer"
    compile "com.android.support:design:$supportLibVer"
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'uk.co.barbuzz:beerprogressview:0.0.4'
}

configurations {
    deployerJar
}

dependencies {
    deployerJar "ar.com.synergian:wagon-git:0.2.5"
}


/**
 *  1. Commit your work (git add . && git status)
 *  2. ./gradlew build uploadArchives -i
 *  3. Push to master and tag
 *
 */
uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {

        pom.groupId = "com.root.shield"
        pom.artifactId = "agent"
        pom.version = "0.1.2"

        // ~/.gradle/gradle.properties (file)
        // BITBUCKET_REPOSITORY_USERNAME=
        // BITBUCKET_REPOSITORY_PASSWORD=

        configuration = configurations.deployerJar
        repository(url: "git:releases://git@bitbucket-private:sirackh/root-d.git"){
            authentication(userName: BITBUCKET_REPOSITORY_USERNAME, password: BITBUCKET_REPOSITORY_PASSWORD)
        }
        snapshotRepository(url: "git@bitbucket-private:sirackh/root-d.git"){
            authentication(userName: BITBUCKET_REPOSITORY_USERNAME, password: BITBUCKET_REPOSITORY_PASSWORD)
        }


        pom.project {
            name "Root Detection"
            packaging "aar"
            description "Root Detection SDK"
            url "https://bitbucket.org/sirackh/root-d/overview"

            scm {
                url "scm:git@bitbucket.org:sirackh/root-d"
                connection "scm:git@bitbucket.org:sirackh/root-d"
                developerConnection "scm:git@bitbucket.org:sirackh/root-d"
            }

            licenses {
                license {
                    name "Sirack"
                    url "http://linkedin.com/in/sirackh"
                    distribution "repo"
                }
            }

            developers {
                developer {
                    id "sirackh"
                    name "Sirack Hailu"
                    email "sirackh@gmail.com"
                }
            }
        }
    }
}
