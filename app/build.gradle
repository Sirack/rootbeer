apply plugin: 'com.android.application'
apply plugin: 'maven'

android {
    compileSdkVersion compileSdkVer

    defaultConfig {
        applicationId "com.scottyab.rootbeer.sample"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        multiDexEnabled true
        versionCode 4
        versionName "0.4"
    }

    //check if the keystore details are defined in gradle.properties (this is so the key is not in github)
    if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
        signingConfigs {
            //from ~/user/.gradle/gradle.properties
            release {
                storeFile file(ROOTBEER_SAMPLE_STORE)
                keyAlias ROOTBEER_SAMPLE_KEY
                storePassword ROOTBEER_SAMPLE_STORE_PASS
                keyPassword ROOTBEER_SAMPLE_KEY_PASS
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        release {
            if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions{
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':rootbeerlib')
    implementation "com.android.support:appcompat-v7:$supportLibVer"
    implementation "com.android.support:multidex:1.0.3"
    implementation "com.android.support:design:$supportLibVer"
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'uk.co.barbuzz:beerprogressview:0.0.4'

    implementation 'org.slf4j:slf4j-api:1.7.21'
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }
    implementation ('com.papertrailapp:logback-syslog4j:1.0.0'){
        exclude group: 'ch.qos.logback'
    }
}